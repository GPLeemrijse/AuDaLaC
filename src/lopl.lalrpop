use std::str::FromStr;
use crate::ast::*;
use lalrpop_util::ParseError;


grammar;

match {
    "struct" => "struct",
    "if" => "if",
    "then" => "then",
    ":=" => ":=",
    "null" => "null",
    "this" => "this",
    "String" => "String",
    "Nat" => "Nat",
    "Int" => "Int",
    "Bool" => "Bool",
    r#""[^"]*""# => STRING,
    r"(0|-?[1-9][0-9]*)" => NUM,
    r"(true|false)" => BOOL,
} else {
    r"[a-zA-Z][a-zA-Z0-9_]*(\.[a-zA-Z][a-zA-Z0-9_]*)*" => ID,
    _
}

Tier<Op, NextTier>: Box<Exp> = {
    Tier<Op, NextTier> Op NextTier => Box::new(Exp::BinOp(<>)),
    NextTier
};

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Var: Vec<String> = {
    ID => <>.to_string().split(".").map(str::to_string).collect()
}

// An identifier which throws a parse error when it contains "."
SimpleID : String = {
    ID =>? {
        if <>.contains(".") {
            Err(ParseError::User {
                error: "Expected Simple ID (= without '.')."
            })
        } else {
            Ok(<>.to_string())
        }
    },
}

Struct: Box<LoplStruct> = {
    "struct" <name:SimpleID> "(" <parameters:Pars> ")" "{" <steps:Steps> "}" => Box::new(LoplStruct{<>})
}

Pars = Comma<Par>;
Par : (String, Type) = <SimpleID> ":" <Type>;

Step: Box<Step> = {
    <name:SimpleID> "{" <statements:Stats> "}" => Box::new(Step {<>})
}

pub Steps: Vec<Box<Step>> = Step*;
pub Structs: Vec<Box<LoplStruct>> = Struct+;

pub Stat: Box<Stat> = {
    "if" <Exp> "then" "{" <Stats> "}" => Box::new(Stat::IfThen(<>)),
    <Type> <SimpleID> ":=" <Exp> ";" => Box::new(Stat::Declaration(<>)),
    <Var> ":=" <Exp> ";" => Box::new(Stat::Assignment(<>)),
}

Stats = Stat*;
Type: Type = {
    "String" => Type::StringType,
    "Nat" => Type::NatType,
    "Int" => Type::IntType,
    "Bool" => Type::BoolType,
    SimpleID => Type::NamedType(<>)
}


// || operator has least precendence
pub Exp = Tier<OrOperator, AndExp>;
AndExp = Tier<AndOperator, EqExp>;
EqExp = Tier<EqualityOperator, CompExp>;
CompExp = Tier<CompareOperator, AddSubExp>;
AddSubExp = Tier<AddSubOperator, MultDivExp>;
MultDivExp = Tier<MultDivModOperator, NegExp>;
NegExp: Box<Exp> = {
    NegationOperator NegExp => Box::new(Exp::UnOp(<>)),
    BottomExp,
}

BottomExp: Box<Exp> = {
    "null" => Box::new(Exp::Lit(Literal::NullLit)),
    "this" => Box::new(Exp::Lit(Literal::ThisLit)),
    NUM => Box::new(Exp::Lit(Literal::NumLit(i64::from_str(<>).unwrap()))),
    STRING => Box::new(Exp::Lit(Literal::StringLit(<>[1..<>.len()-1].to_string()))),
    BOOL => Box::new(Exp::Lit(Literal::BoolLit(<>.parse().unwrap()))),
    <SimpleID> "(" <Exps> ")" => Box::new(Exp::Constructor(<>)),
    Var => Box::new(Exp::Var(<>)),
    "(" <Exp> ")"
}

Exps = Comma<Exp>;


NegationOperator: UnOpcode = {
    "!" => UnOpcode::Negation
}

MultDivModOperator: BinOpcode = {
    "*" => BinOpcode::Mult,
    "/" => BinOpcode::Div,
    "%" => BinOpcode::Mod
}

AddSubOperator: BinOpcode = {
    "+" => BinOpcode::Plus,
    "-" => BinOpcode::Minus
}


CompareOperator: BinOpcode = {
    "<=" => BinOpcode::LessThanEquals,
    ">=" => BinOpcode::GreaterThanEquals,
    "<" => BinOpcode::LessThan,
    ">" => BinOpcode::GreaterThan,
}

EqualityOperator: BinOpcode = {
    "==" => BinOpcode::Equals,
    "!=" => BinOpcode::NotEquals,
}

AndOperator: BinOpcode = {
    "&&" => BinOpcode::And
}

OrOperator: BinOpcode = {
    "||" => BinOpcode::Or
}

Num: i32 = <s:r"[0-9]+"> => i32::from_str(s).unwrap();
