$$__device__ uint nrof_components = 0;

struct Edge(s : Node, t : Node, deleted: Bool){

	propagate {
		if !deleted then {
			if s.fwd < t.fwd then {
				t.fwd := s.fwd;
			}
			if t.bwd < s.bwd then {
				s.bwd := t.bwd;
			}
		}
	}

	delete_frontier_edges {
		if !deleted then {
			deleted := s.fwd != t.fwd || s.bwd != t.bwd;
		}
	}
}

struct Node(fwd : Node, bwd : Node){

	init {
		fwd := this;
		bwd := this;
	}

	reset_fwd_bwd {
		if fwd != bwd then {
			fwd := this;
			bwd := this;
		}
	}

	count_if_scc {
		if this != null && fwd == this && fwd == bwd then {
			$$atomicInc(&nrof_components, 0xffffffff);
		}
	}

	print_components {
		if this == null then {
			$$printf("Nrof components: %u\n", nrof_components);
		}
	}
}

init <
Fix(
	Fix(propagate) <
	delete_frontier_edges <
	reset_fwd_bwd
)
 < count_if_scc
 < print_components